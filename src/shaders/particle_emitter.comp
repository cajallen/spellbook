#version 450
#pragma shader_stage(compute)

struct Particle {
    vec4 position_scale;
    vec4 velocity_damping;
    float color_x;
    float life;
    float life_total;
    float falloff;
};

layout(binding = 0) buffer Particles {
    uint head;
    Particle particles[];
};

layout(binding = 1) uniform EmitterSettings {
    vec4 position_scale;
    vec4 velocity_damping;

    vec4 position_scale_random;
    vec4 velocity_damping_random;
    
    float life;
    float life_random;
    float falloff;
    
    uint max_particles;
};

layout(push_constant) uniform uPushConstant {
    uint spawn_count;
    float dt;
} pc;


uint uint_noise(uint i, uint seed) {
    uint mangled = i;
    mangled *= 0xB5297A4Du;
    mangled += seed;
    mangled ^= (mangled >> 8);
    mangled += 0x68E31DA4u;
    mangled ^= (mangled << 8);
    mangled *= 0x1B56C4E9u;
    mangled ^= (mangled >> 8);
    return mangled;
}

float float_noise(uint i, uint seed) {
    return float(uint_noise(i, seed) & 0xffffffu) / 16777215.0;
}

layout (local_size_x = 64) in;
void main() {
    uint index = gl_GlobalInvocationID.x;

    bool spawn1 = head <= index && index < head + pc.spawn_count;
    bool spawn2 = (head + pc.spawn_count > max_particles) && index < ((head + pc.spawn_count) % max_particles);

    float pr0 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 0);
    float pr1 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 1);
    float pr2 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 2);
    float pr3 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 3);

    float vr4 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 4);
    float vr5 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 5);
    float vr6 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 6);
    float vr7 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 7);

    float lr8 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 8);
    
    float cr9 = float_noise(uint(pc.dt * 43758.5453), index * 10 + 9);

    if (spawn1 || spawn2) {
        particles[index] = Particle(
            position_scale + vec4(pr0, pr1, pr2, pr3) * position_scale_random,
            velocity_damping + vec4(vr4, vr5, vr6, vr7) * velocity_damping_random,
            cr9,
            life + lr8 * life_random,
            life + lr8 * life_random,
            falloff
        );
    } else {
        particles[index].velocity_damping.xyz *= pow(particles[index].velocity_damping.w, pc.dt);
        particles[index].position_scale.xyz += particles[index].velocity_damping.xyz * pc.dt;
        particles[index].life -= pc.dt;
    }
    
    if (index == head) {
        head = (head + pc.spawn_count) % max_particles;
    }
}